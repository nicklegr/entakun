# in ms
followee_update_interval = 5 * 60 * 1000

load_followees = () ->
  followees = $.cookie("entakun-followees")

  if followees == null
    followees = []

  followees

save_followees = (list) ->
  $.cookie("entakun-followees", list)

remove_followee = (project, staff) ->
  followees = load_followees()
  followees = $.grep(followees, (e) ->
    !(e['project'] == project && e['staff'] == staff)
  )
  save_followees(followees)

lookup_followees = () ->
  followees_data = []

  followees = load_followees()
  if followees.length > 0
    $.ajax({
      async: false,
      type: "GET",
      url: '<%= url_for "/lookup_followees" %>',
      data: { followees: followees },
      dataType: 'json',
    }).done((data) ->
      followees_data = data
    )

  followees_data

interval_update_followees = () ->
  $("body").everyTime(followee_update_interval, () ->
    update_followees()
  )

setup_followees = () ->
  elems = $.grep($('#followees > .followee'), (e) ->
    $(e).attr('id') != 'followee-template'
  )
  $(elems).remove()

  followees_data = lookup_followees()
  $.each(followees_data, (i) ->
    if this.staff_name
      add_followee_html(this.project, this.staff, this.staff_name, this.task_name)
    else
      # staff deleted. remove from cookie
      remove_followee(this.project, this.staff)
  )

update_followees = () ->
  followees_data = lookup_followees()
  $.each(followees_data, (i) ->
    data = this

    followee = $.grep($("#followees > .followee"), (e) ->
      if $(e).attr('id') == 'followee-template'
        false
      else
        $(e).data('followee')['project'] == data.project &&
        $(e).data('followee')['staff'] == data.staff
    )

    if data.staff_name
      $(followee).find('.followee-name > .name').text(data.staff_name)

      if data.task_name
        $(followee).find('.task').show()
        $(followee).find('.task > .name').text(data.task_name)
      else
        # task deleted or unassigned
        $(followee).find('.task').hide()
    else
      # staff deleted
      $(followee).remove()
      remove_followee(data.project, data.staff)
  )

add_followee_html = (project_key, staff_id, staff_name, task_name) ->
  # copy element
  new_followee = $('#followee-template').clone()
  new_followee.removeAttr('id') # don't set. to avoid dup id with staff
  new_followee.find('.followee-name > .name').text(staff_name)
  new_followee.show()

  new_followee.data('followee', { project: project_key, staff: staff_id })

  if task_name
    new_followee.find('.task > .name').text(task_name)
  else
    new_followee.find('.task').hide()

  # in-place delete
  name = new_followee.find('.followee-name')
  name.find('.delete').click(()->
    # remove cookie
    data = new_followee.data('followee')
    remove_followee(data['project'], data['staff'])

    # fade out
    $(this).parents('.followee').effect('fade', {}, 300, () ->
      $(this).remove()
    )
  )

  # show delete icon when mouse hover
  name.hover(
    () ->
      $(this).find('.delete').show()
    ,
    () ->
      $(this).find('.delete').hide()
  )

  $("#followees").append(new_followee)
  new_followee
